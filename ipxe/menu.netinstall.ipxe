#!ipxe

# To access this file via http use following redirection:
# http://preseed.panticz.de/ipxe/menu.netinstall.ipxe

# choose cpu architecture
cpuid --ext 29 && set arch amd64 || set arch i386

menu NetInstall
item --gap NetInstall Ubuntu Preseed (user: ubuntu, password: ubuntu):
item install_ubuntu_19.10           Ubuntu 19.10 Eoan
item install_ubuntu_19.04           Ubuntu 19.04 Disco
item install_ubuntu_18.04           Ubuntu 18.04 LTS Bionic
item install_ubuntu_16.04           Ubuntu 16.04 LTS Xenial
item
item --gap NetInstall Ubuntu Preseed autopart (ALL DATA WIL BY REMOVED!) (user: ubuntu, password: t00r):
item install_ubuntu_18.04_autopart  Ubuntu 18.04 LTS Bionic autopart
item install_ubuntu_16.04_autopart  Ubuntu 16.04 LTS Xenial autopart
item
item --gap NetInstall Debian Preseed (user: root, password: t00r):
item install_debian_10              Debian 10 Buster
item install_debian_9               Debian 9 Stretch
item install_debian_8               Debian 8 Jessie
item
item --gap NetInstall Debian (manual installation)
item install_debian_10_not_preseed   Debian 10 Buster
item
item --gap NetInstall Fedora:  
item install_fedora_25              Fedora 25
item install_fedora_25_kickstart    Fedora 25 (kickstart)
item
item --gap NetInstall CentOS:  
item install_centos_7             CentOS 7
item install_centos_7_kickstart   CentOS 7 (kickstart)
item
item --gap NetInstall Arch Linux:  
item install_arch_linux             Arch Linux Netboot
item
item --gap Other
item install_ipfire                 IpFire
item
item back   < Back to main menu
choose target && goto ${target}

:back
isset ${mainmenu} || set mainmenu http://preseed.panticz.de/ipxe/menu.ipxe
chain ${mainmenu}


#
# Ubuntu
#
:install_ubuntu_19.10
set release eoan
goto install_ubuntu

:install_ubuntu_19.04
set release disco
goto install_ubuntu

:install_ubuntu_18.04
set release bionic
goto install_ubuntu

:install_ubuntu_18.04_autopart
set autopart autopart
set release bionic
goto install_ubuntu

:install_ubuntu_16.04
set release xenial
goto install_ubuntu

:install_ubuntu_16.04_autopart
set autopart autopart
set release xenial
goto install_ubuntu

:install_ubuntu
initrd http://archive.ubuntu.com/ubuntu/dists/${release}/main/installer-${arch}/current/images/netboot/ubuntu-installer/${arch}/initrd.gz
chain http://archive.ubuntu.com/ubuntu/dists/${release}/main/installer-${arch}/current/images/netboot/ubuntu-installer/${arch}/linux url=http://preseed.panticz.de/preseed/ubuntu-minimal.seed auto=true netcfg/choose_interface=${net0/mac} locale=en_US.UTF-8 priority=critical net.ifnames=0 biosdevname=0 ${autopart} ${options}

#
# Debian
#
:install_debian_10
set release buster
goto install_debian_preseed

:install_debian_10_manual
set release buster
goto install_debian

:install_debian_9
set release stretch
goto install_debian_preseed

:install_debian_8
set release jessie
goto install_debian_preseed

:install_debian
initrd http://cdn.debian.net/debian/dists/${release}/main/installer-${arch}/current/images/netboot/debian-installer/${arch}/initrd.gz
chain http://cdn.debian.net/debian/dists/${release}/main/installer-${arch}/current/images/netboot/debian-installer/${arch}/linux

:install_debian_preseed
initrd http://cdn.debian.net/debian/dists/${release}/main/installer-${arch}/current/images/netboot/debian-installer/${arch}/initrd.gz
chain http://cdn.debian.net/debian/dists/${release}/main/installer-${arch}/current/images/netboot/debian-installer/${arch}/linux url=http://preseed.panticz.de/preseed/debian-minimal.seed auto=true netcfg/choose_interface=${net0/mac} priority=critical ${autopart}

#
# Fedora
#
:install_fedora_25
set release 25
goto install_fedora

:install_fedora_25_kickstart
set release 25
set ks http://preseed.panticz.de/ks/fedora-minimal.ks
goto install_fedora

:install_fedora
initrd http://ftp.halifax.rwth-aachen.de/fedora/linux/releases/${release}/Workstation/x86_64/os/images/pxeboot/initrd.img
chain http://ftp.halifax.rwth-aachen.de/fedora/linux/releases/${release}/Workstation/x86_64/os/images/pxeboot//vmlinuz ksdevice=link inst.repo=http://ftp.halifax.rwth-aachen.de/fedora/linux/releases/${release}/Workstation/x86_64/os/ ks=${ks}



#
# CentOS
#
# TODO: auto 32/64 bit
:install_centos_7
initrd http://mirror.netcologne.de/centos/7/os/x86_64/isolinux/initrd.img
chain http://mirror.netcologne.de/centos/7/os/x86_64/isolinux/vmlinuz

:install_centos_7_kickstart
initrd http://mirror.netcologne.de/centos/7/os/x86_64/isolinux/initrd.img
chain http://mirror.netcologne.de/centos/7/os/x86_64/isolinux/vmlinuz ksdevice=link edd=off ks=http://preseed.panticz.de/kickstart/centos7-minimal.cfg net.ifnames=0 biosdevname=0

#
# Arch Linux
#
:install_arch_linux
cpuid --ext 29 && set arch x86_64 || set arch i686
initrd http://mirror.netcologne.de/archlinux/iso/latest/arch/boot/${arch}/archiso.img
chain http://mirror.netcologne.de/archlinux/iso/latest/arch/boot/${arch}/vmlinuz autopartarchiso_http_srv=http://mirror.netcologne.de/archlinux/iso/latest/ archisobasedir=arch checksum=y ip=dhcp

#
# IpFire
#
:install_ipfire
chain http://boot.ipfire.org/menu.gpxe
